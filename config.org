#+TITLE:A GNU Emacs Config
#+AUTHOR: Kay Zero
#+DESCRIPTION: k's personal Emacs config.

* Table Of Contents :toc:
- [[#important-programs-to-load-first][Important Programs To Load First]]
  - [[#elpaca-package-manager][Elpaca Package Manager]]
  - [[#mood-line][Mood Line]]
  - [[#evil-mode][Evil Mode]]
- [[#auto-lightdark-theme-in-emacs][Auto Light/Dark Theme in Emacs]]
- [[#comments][Comments]]
- [[#completion-framework-modern][Completion Framework (Modern)]]
  - [[#overview][Overview]]
  - [[#components][Components]]
  - [[#benefits][Benefits]]
  - [[#setup][Setup]]
  - [[#lsp-setup][LSP Setup]]
- [[#dired][Dired]]
- [[#eldoc][ElDoc]]
- [[#embark][Embark]]
- [[#folding][Folding]]
- [[#languages][Languages]]
  - [[#bash][Bash]]
  - [[#cc][C/C++]]
  - [[#golang][Golang]]
  - [[#lua][Lua]]
  - [[#julia][Julia]]
  - [[#python][Python]]
  - [[#latex][Latex]]
- [[#magit][Magit]]
- [[#markdown][Markdown]]
- [[#my-functions][My Functions]]
- [[#org-mode][ORG MODE]]
  - [[#org-mode-source-block][Org Mode Source Block]]
  - [[#org-tempo][Org Tempo]]
  -  [[#table-of-contents][Table of Contents]]
  - [[#general-org-mode-settings][General Org Mode Settings]]
- [[#sane-defaults][Sane Defaults]]
  - [[#fixing-emacs-wierdness][Fixing Emacs Wierdness]]
  - [[#treesitter][Treesitter]]
- [[#vterm][Vterm]]
- [[#vundo][Vundo]]
- [[#zoxide][Zoxide]]
- [[#keybindings][Keybindings]]
  - [[#emacs-specific][EMACS Specific]]
  - [[#evil-mode-specific][EVIL Mode Specific]]
- [[#ui][UI]]
  - [[#theme][Theme]]
  - [[#fonts][FONTS]]
  - [[#graphical-user-interface-tweaks][GRAPHICAL USER INTERFACE TWEAKS]]
- [[#testing][Testing]]

* Important Programs To Load First
** Elpaca Package Manager 

#+begin_src emacs-lisp
  ;;; Elpaca Setup
  (defvar elpaca-installer-version 0.11)
  (defvar elpaca-directory (expand-file-name "elpaca/" user-emacs-directory))
  (defvar elpaca-builds-directory (expand-file-name "builds/" elpaca-directory))
  (defvar elpaca-repos-directory (expand-file-name "repos/" elpaca-directory))
  (defvar elpaca-order '(elpaca :repo "https://github.com/progfolio/elpaca.git"
                                :ref nil :depth 1 :inherit ignore
                                :files (:defaults "elpaca-test.el" (:exclude "extensions"))
                                :build (:not elpaca--activate-package)))
  (let* ((repo  (expand-file-name "elpaca/" elpaca-repos-directory))
         (build (expand-file-name "elpaca/" elpaca-builds-directory))
         (order (cdr elpaca-order))
         (default-directory repo))
    (add-to-list 'load-path (if (file-exists-p build) build repo))
    (unless (file-exists-p repo)
      (make-directory repo t)
      (when (<= emacs-major-version 28) (require 'subr-x))
      (condition-case-unless-debug err
          (if-let* ((buffer (pop-to-buffer-same-window "*elpaca-bootstrap*"))
                    ((zerop (apply #'call-process `("git" nil ,buffer t "clone"
                                                    ,@(when-let* ((depth (plist-get order :depth)))
                                                        (list (format "--depth=%d" depth) "--no-single-branch"))
                                                    ,(plist-get order :repo) ,repo))))
                    ((zerop (call-process "git" nil buffer t "checkout"
                                          (or (plist-get order :ref) "--"))))
                    (emacs (concat invocation-directory invocation-name))
                    ((zerop (call-process emacs nil buffer nil "-Q" "-L" "." "--batch"
                                          "--eval" "(byte-recompile-directory \".\" 0 'force)")))
                    ((require 'elpaca))
                    ((elpaca-generate-autoloads "elpaca" repo)))
              (progn (message "%s" (buffer-string)) (kill-buffer buffer))
            (error "%s" (with-current-buffer buffer (buffer-string))))
        ((error) (warn "%s" err) (delete-directory repo 'recursive))))
    (unless (require 'elpaca-autoloads nil t)
      (require 'elpaca)
      (elpaca-generate-autoloads "elpaca" repo)
      (let ((load-source-file-function nil)) (load "./elpaca-autoloads"))))
  (add-hook 'after-init-hook #'elpaca-process-queues)
  (elpaca `(,@elpaca-order))


  ;; Install a package via the elpaca macro
  ;; See the "recipes" section of the manual for more details.

  ;; (elpaca example-package)

  ;; Install use-package support
  (elpaca elpaca-use-package
    ;; Enable use-package :ensure support for Elpaca.
    (elpaca-use-package-mode))

  ;;When installing a package used in the init file itself,
  ;;e.g. a package which adds a use-package key word,
  ;;use the :wait recipe keyword to block until that package is installed/configured.
  ;;For example:
  ;;(use-package general :ensure (:wait t) :demand t)

  ;; Expands to: (elpaca evil (use-package evil :demand t))
  ;; (use-package evil :ensure t :demand t)

  ;;Turns off elpaca-use-package-mode current declaration
  ;;Note this will cause evaluate the declaration immediately. It is not deferred.
  ;;Useful for configuring built-in emacs features.
  (use-package emacs :ensure nil :config (setq ring-bell-function #'ignore))

#+end_src



** Mood Line
A lightweight, drop-in replacement for the default Emacs mode line configuration.
Probably faster than the default moed line 
#+begin_src emacs-lisp
  (use-package mood-line
   :ensure t 
   :config
   (mood-line-mode 1)
   :custom
   (mood-line-glyph-alist mood-line-glyphs-fira-code))
#+end_src

** Evil Mode

#+begin_src emacs-lisp


  ;;; Evil Package Vim Bindings
  (use-package evil
    :ensure t
    :demand t
    :bind (("<escape>" . keyboard-escape-quit))
    :init      ;; tweak evil's configuration before loading it
    ;; allows for using cgn
    ;; (setq evil-search-module 'evil-search)
    (setq evil-want-keybinding nil)
    ;; no vim insert bindings
    (setq evil-want-integration t) ;; This is optional since it's already set to t by default.
    (setq evil-vsplit-window-right t)
    (setq evil-split-window-below t)
    :config
    (evil-mode 1))



  (use-package evil-collection
    :after evil
    :demand t
    :ensure t
    :custom
    (evil-collection-mode-list '(dashboard dired ibuffer info woman help))
    (evil-collection-key-blacklist '("SPC"))   ;; Free space for leader key
    :config
    (evil-collection-init)

    ;; Your actual useful overrides only
    (evil-collection-define-key 'normal 'dired-mode-map
      "h" 'dired-up-directory
      "l" 'dired-find-file)

    (evil-collection-define-key 'normal 'Info-mode-map
      "h" 'Info-history-back
      "l" 'Info-history-forward
      "o" 'Info-menu))

#+end_src

* Auto Light/Dark Theme in Emacs
#+begin_src emacs-lisp
  (use-package auto-dark
    :ensure t
    :demand t
    :custom
    ;; Dark mode → modus-vivendi; Light mode → modus-operandi
    (auto-dark-themes '((modus-vivendi) (modus-operandi)))
    ;; How often to check for system theme change (in seconds)
    (auto-dark-polling-interval-seconds 5)
    ;; Don’t use AppleScript or PowerShell 
    (auto-dark-allow-osascript nil)
    (auto-dark-allow-powershell nil)
    ;; Optional: only if you know what you're doing
    ;; (auto-dark-detection-method nil)
    :hook
    (auto-dark-dark-mode
     . (lambda ()
         (message "Switched to dark theme ☪")))
    (auto-dark-light-mode
     . (lambda ()
         (message "Switched to light theme ☼")))
    :init
    (auto-dark-mode))


#+end_src

* Comments
#+begin_src emacs-lisp
(use-package evil-nerd-commenter
  :ensure t
  :after evil
  :config
  (evilnc-default-hotkeys))

#+end_src

* Completion Framework (Modern)
** Overview
Modern completion stack providing superior search, filtering, and UI,Prefering features/plugins prioritizing
emacs inbuilt features

** Components
- ~consult~ :: Enhanced incremental search and completion commands  
- ~vertico~ :: Vertical completion UI with smooth scrolling and history
- ~marginalia~ :: Rich annotations (file size, git status, etc.)
- ~orderless~ :: Intelligent multi-word fuzzy pattern matching

** Benefits
- ~10x~ faster than ido-everywhere
- Consistent across ~M-x~, files, buffers, symbols
- Visual feedback with preview windows
- Extensible for project tools (projectile, lsp)

** Setup
#+begin_src emacs-lisp

  ;; Completion framework setup

  (use-package vertico
    :ensure t
    :custom
    (vertico-cycle t)
    :init
    (vertico-mode))

  ;; Enable rich annotations using the Marginalia package
  (use-package marginalia
    :ensure t
    ;; Bind `marginalia-cycle' locally in the minibuffer.  To make the binding
    ;; available in the *Completions* buffer, add it to the
    ;; `completion-list-mode-map'.
    :bind (:map minibuffer-local-map
                ("M-A" . marginalia-cycle))
    
    ;; The :init section is always executed.
    :init
    
    ;; Marginalia must be activated in the :init section of use-package such that
    ;; the mode gets enabled right away. Note that this forces loading the
    ;; package.
    (marginalia-mode))

  (use-package orderless
    :ensure t
    :custom
    (completion-styles '(orderless flex))
    (completion-category-overrides '((file (styles partial-completion)))))


  ;; Save completion history
  (use-package savehist
    :init
    (savehist-mode))

  ;; Recent files setup
  (use-package recentf
    :custom
    (recentf-max-saved-items 200)
    (recentf-exclude '("/tmp/" "/var/folders/" "\\.?cache" "__pycache__"))
    :init
    (recentf-mode))



  ;; Example configuration for Consult
  (use-package consult
    ;; Replace bindings. Lazily loaded by `use-package'.
    :bind (;; C-c bindings in `mode-specific-map'
           ("C-c M-x" . consult-mode-command)
           ("C-c h" . consult-history)
           ("C-c k" . consult-kmacro)
           ("C-c m" . consult-man)
           ("C-c i" . consult-info)
           ([remap Info-search] . consult-info)
           ;; C-x bindings in `ctl-x-map'
           ("C-x M-:" . consult-complex-command)     ;; orig. repeat-complex-command
           ("C-x b" . consult-buffer)                ;; orig. switch-to-buffer
           ("C-x 4 b" . consult-buffer-other-window) ;; orig. switch-to-buffer-other-window
           ("C-x 5 b" . consult-buffer-other-frame)  ;; orig. switch-to-buffer-other-frame
           ("C-x t b" . consult-buffer-other-tab)    ;; orig. switch-to-buffer-other-tab
           ("C-x r b" . consult-bookmark)            ;; orig. bookmark-jump
           ("C-x p b" . consult-project-buffer)      ;; orig. project-switch-to-buffer
           ;; Custom M-# bindings for fast register access
           ("M-#" . consult-register-load)
           ("M-'" . consult-register-store)          ;; orig. abbrev-prefix-mark (unrelated)
           ("C-M-#" . consult-register)
           ;; Other custom bindings
           ("M-y" . consult-yank-pop)                ;; orig. yank-pop
           ;; M-g bindings in `goto-map'
           ("M-g e" . consult-compile-error)
           ("M-g r" . consult-grep-match)
           ("M-g f" . consult-flymake)               ;; Alternative: consult-flycheck
           ("M-g g" . consult-goto-line)             ;; orig. goto-line
           ("M-g M-g" . consult-goto-line)           ;; orig. goto-line
           ("M-g o" . consult-outline)               ;; Alternative: consult-org-heading
           ("M-g m" . consult-mark)
           ("M-g k" . consult-global-mark)
           ("M-g i" . consult-imenu)
           ("M-g I" . consult-imenu-multi)
           ;; M-s bindings in `search-map'
           ("M-s d" . consult-find)                  ;; Alternative: consult-fd
           ("M-s c" . consult-locate)
           ("M-s g" . consult-grep)
           ("M-s G" . consult-git-grep)
           ("M-s r" . consult-ripgrep)
           ("M-s l" . consult-line)
           ("M-s L" . consult-line-multi)
           ("M-s k" . consult-keep-lines)
           ("M-s u" . consult-focus-lines)
           ;; Isearch integration
           ("M-s e" . consult-isearch-history)
           :map isearch-mode-map
           ("M-e" . consult-isearch-history)         ;; orig. isearch-edit-string
           ("M-s e" . consult-isearch-history)       ;; orig. isearch-edit-string
           ("M-s l" . consult-line)                  ;; needed by consult-line to detect isearch
           ("M-s L" . consult-line-multi)            ;; needed by consult-line to detect isearch
           ;; Minibuffer history
           :map minibuffer-local-map
           ("M-s" . consult-history)                 ;; orig. next-matching-history-element
           ("M-r" . consult-history))                ;; orig. previous-matching-history-element
    
    ;; Enable automatic preview at point in the *Completions* buffer. This is
    ;; relevant when you use the default completion UI.
    :hook (completion-list-mode . consult-preview-at-point-mode)
    
    ;; The :init configuration is always executed (Not lazy)
    :init
    
    ;; Tweak the register preview for `consult-register-load',
    ;; `consult-register-store' and the built-in commands.  This improves the
    ;; register formatting, adds thin separator lines, register sorting and hides
    ;; the window mode line.
    (advice-add #'register-preview :override #'consult-register-window)
    (setq register-preview-delay 0.5)
    
    ;; Use Consult to select xref locations with preview
    (setq xref-show-xrefs-function #'consult-xref
          xref-show-definitions-function #'consult-xref)
    
    ;; Configure other variables and modes in the :config section,
    ;; after lazily loading the package.
    :config
    
    ;; Optionally configure preview. The default value
    ;; is 'any, such that any key triggers the preview.
    ;; (setq consult-preview-key 'any)
    ;; (setq consult-preview-key "M-.")
    ;; (setq consult-preview-key '("S-<down>" "S-<up>"))
    ;; For some commands and buffer sources it is useful to configure the
    ;; :preview-key on a per-command basis using the `consult-customize' macro.
    (consult-customize
     consult-theme :preview-key '(:debounce 0.2 any)
     consult-ripgrep consult-git-grep consult-grep consult-man
     consult-bookmark consult-recent-file consult-xref
     consult-source-bookmark consult-source-file-register
     consult-source-recent-file consult-source-project-recent-file
     ;; :preview-key "M-."
     :preview-key '(:debounce 0.4 any))
    
    ;; Optionally configure the narrowing key.
    ;; Both < and C-+ work reasonably well.
    (setq consult-narrow-key "<") ;; "C-+"
    
    ;; Optionally make narrowing help available in the minibuffer.
    ;; You may want to use `embark-prefix-help-command' or which-key instead.
    ;; (keymap-set consult-narrow-map (concat consult-narrow-key " ?") #'consult-narrow-help)
    )





  (use-package consult-dir
    :ensure t
    :bind (("C-x C-d" . consult-dir)
           :map vertico-map
           ("C-x C-d" . consult-dir)
               ("C-x C-j" . consult-dir-jump-file)))
#+end_src



** LSP Setup
This section configures the LSP and completion setup:
- Eglot: built-in, lightweight LSP client for language servers.
- Corfu: fast and minimal completion popup UI.
- Yasnippet: snippet expansion engine; yasnippet-snippets adds community snippets.
- Cape: extends completion-at-point functions with useful sources (dabbrev, file, etc.).
- Yasnippet-capf: integrates snippets into the completion framework.
- Custom hook integrates LSP, snippets, and file completions for a unified experience.

#+begin_src emacs-lisp

    ; Core LSP: Eglot (built-in, fast)
    (use-package eglot
      :custom
      ;; Performance optimizations
      (eglot-sync-connect 1)           ; Quick connect then async
      (eglot-autoshutdown t)           ; Kill idle servers
      (eglot-ignored-server-capabilities '(:documentHighlightProvider))  ; Skip unused
      :hook
      ;; Enable in Tree-sitter modes
      (((prog-mode text-mode) . eglot-ensure)
       (python-ts-mode . eglot-ensure)
       (lua-ts-mode . eglot-ensure)
       (js-ts-mode . eglot-ensure)
       (typescript-ts-mode . eglot-ensure)
       (julia-ts-mode . eglot-ensure)
       (rust-ts-mode . eglot-ensure))) 



    ;; Fast completion: Corfu
    (use-package corfu
      :ensure t
      :custom
      (corfu-auto t)                   ; Auto popup
      (corfu-cycle t)                  ; Cycle candidates
      (corfu-auto-prefix 1)            ; No or chars before auto-pop-up shows
      (corfu-quit-no-match t)          ; Quit if no match
      :config
      (setq corfu-auto-delay 0.5) ; Controls delay for popup-info-mode eldoc  
      (setq corfu-popupinfo-delay 0.1) ; Controls delay for popup-info-mode eldoc  
      (global-corfu-mode)
      (corfu-popupinfo-mode)
  	(corfu-history-mode)
  	(evil-collection-corfu-setup))

    (use-package yasnippet
      :ensure t
      :config
      (yas-global-mode 1))  ;; Enable Yasnippet globally

    (use-package yasnippet-snippets  ;; Optional: Provides a collection of snippets
      :ensure t)

    (use-package yasnippet-capf
      :ensure t
      :after (yasnippet cape))


    (use-package cape
      :ensure t
      :init
      ;; Add general Cape completions
      (add-hook 'completion-at-point-functions #'cape-file)
      (add-hook 'completion-at-point-functions #'cape-dabbrev)
      (add-hook 'completion-at-point-functions #'cape-elisp-block)
      (add-hook 'completion-at-point-functions #'cape-history)
    )

    ;; Integrate LSP, snippets, and file completions in Eglot-managed modes

  (defun my/eglot-completion-setup ()
    (setq-local completion-at-point-functions
                (append
                 ;; These get highest priority 
                 (list
                  (cape-capf-super
                   #'yasnippet-capf
                   #'eglot-completion-at-point
                   ))
                 ;; Keep everything that was already there 
                 completion-at-point-functions)))

  ;; Run it every time Eglot takes over a buffer
  (add-hook 'eglot-managed-mode-hook #'my/eglot-completion-setup)
    

#+end_src

* Dired
Sorting In Dired. Also can be done with C-u s --> "-l<>" =note -l is required=

#+begin_src emacs-lisp
  (setq dired-listing-switches "-Al --group-directories-first -v") ;; Set default dired view
  (add-hook 'dired-mode-hook #'dired-hide-details-mode)

#+end_src

* ElDoc
#+begin_src emacs-lisp

  (use-package eldoc
    :defer t
    :custom
    (eldoc-idle-delay 0.1)  ; delay before popup
    (eldoc-message-function #'ignore)        ; don't print in echo area
    (eldoc-documentation-strategy #'eldoc-documentation-compose-eagerly)) ; don't collect info

                  ;; eldoc-box: popup ui for hover
  (elpaca eldoc-box
    :defer t
    :ensure t
    :config
    ;; enable markdown rendering for eglot hovers
    (setq eldoc-box-hover-render-function #'eldoc-box-hover-markdown))


  ;; Eldoc backend that shows full docs for any elisp symbol
  (defun my/elisp-docs (callback &rest _)
    "Show full Elisp documentation in Eldoc / Eldoc-box for the symbol at point."
    (when-let ((sym (symbol-at-point))
               (buf (elisp--company-doc-buffer (symbol-name sym))))
      (funcall callback
               (with-current-buffer buf (buffer-string))
               '(:thing ,(format "%s" sym)))))

  ;; Add it to eldoc functions in emacs-lisp-mode and lisp-interaction-mode
  (add-hook 'emacs-lisp-mode-hook
            (lambda ()
              (add-hook 'eldoc-documentation-functions #'my/elisp-docs nil t)))

  (add-hook 'lisp-interaction-mode-hook
            (lambda ()
              (add-hook 'eldoc-documentation-functions #'my/elisp-docs nil t)))

#+end_src


* Embark
Provides actions on selection targets in buffer/minibuffer (kind of like a right click context menu)
#+begin_src emacs-lisp

  (use-package embark
    :defer t
    :ensure t
    :bind
    (("C-." . embark-act)         ;; pick some comfortable binding
     ("M-SPC" . embark-act)
     ("C-;" . embark-dwim)        ;; good alternative: M-.
     ("C-h B" . embark-bindings)) ;; alternative for `describe-bindings'

    :init

    ;; Optionally replace the key help with a completing-read interface
    (setq prefix-help-command #'embark-prefix-help-command)

    ;; Show the Embark target at point via Eldoc. You may adjust the
    ;; Eldoc strategy, if you want to see the documentation from
    ;; multiple providers. Beware that using this can be a little
    ;; jarring since the message shown in the minibuffer can be more
    ;; than one line, causing the modeline to move up and down:

    ;; (add-hook 'eldoc-documentation-functions #'embark-eldoc-first-target)
    ;; (setq eldoc-documentation-strategy #'eldoc-documentation-compose-eagerly)

    ;; Add Embark to the mouse context menu. Also enable `context-menu-mode'.
    ;; (context-menu-mode 1)
    ;; (add-hook 'context-menu-functions #'embark-context-menu 100)

    :config

    ;; Hide the mode line of the Embark live/completions buffers
    (add-to-list 'display-buffer-alist
                 '("\\`\\*Embark Collect \\(Live\\|Completions\\)\\*"
                   nil
                   (window-parameters (mode-line-format . none)))))

  ;; Consult users will also want the embark-consult package.
  (use-package embark-consult
    :ensure t ; only need to install it, embark loads it after consult if found
    :hook
    (embark-collect-mode . consult-preview-at-point-mode))
#+end_src


* Folding

#+begin_src emacs-lisp
    ;; Enable folding in programming modes
  (add-hook 'prog-mode-hook #'hs-minor-mode)
  ;; Enable outline folding in Org and text modes
  (add-hook 'org-mode-hook #'outline-minor-mode)
  (add-hook 'text-mode-hook #'outline-minor-mode)

#+end_src


* Languages
** Bash
** C/C++
** Golang
** Lua
** Julia
#+begin_src emacs-lisp

(use-package julia-mode
  :ensure t)
  
  (use-package julia-ts-mode
    :defer t
    :ensure t
    :mode "\\.jl$")

  (use-package eglot-jl
    :ensure t
    :after eglot)

  (use-package julia-repl
    :ensure t
    :after julia-ts-mode
    :commands julia-repl
    :custom
    (julia-repl-executable-records '((default "julia")))
    (julia-repl-switches "--startup-file=no --history-file=yes")
    :bind
    (:map julia-ts-mode-map
          ("C-c C-z" . julia-repl)
          ("C-c C-c" . julia-repl-send-buffer)
          ("C-c C-r" . julia-repl-send-region-or-line)
          ("C-c C-l" . julia-repl-send-line)))




#+end_src

#+RESULTS:

** Python
** Latex
#+begin_src emacs-lisp


  (use-package latex
    :ensure auctex
    :mode ("\\.tex\\'" . LaTeX-mode)
    :hook ((LaTeX-mode . prettify-symbols-mode))
    :bind (:map LaTeX-mode-map
                ("C-S-e" . latex-math-from-calc))
    :init
    (setq TeX-auto-save t)
    (setq TeX-parse-self t)
    (setq-default TeX-master nil)
    :config
    ;; Format math as a Latex string with Calc
    (defun latex-math-from-calc ()
      "Evaluate `calc' on the contents of line at point."
      (interactive)
      (cond ((region-active-p)
             (let* ((beg (region-beginning))
                    (end (region-end))
                    (string (buffer-substring-no-properties beg end)))
               (kill-region beg end)
               (insert (calc-eval `(,string calc-language latex
                                            calc-prefer-frac t
                                            calc-angle-mode rad)))))
            (t (let ((l (thing-at-point 'line)))
                 (end-of-line 1) (kill-line 0) 
                 (insert (calc-eval `(,l
                                      calc-language latex
                                      calc-prefer-frac t
                                      calc-angle-mode rad))))))))

  ;;; Synctex + Zathura Support
  (with-eval-after-load 'tex
    (add-to-list 'TeX-view-program-list
                 '("Zathura"
                   ("zathura %o"
                    (mode-io-correlate
                     " --synctex-forward %n:0:%b -x \"emacsclient +%{line} %{input}\""))
                   "zathura"))
    
    (setq TeX-view-program-selection
          '((output-pdf "Zathura")))
    
    (setq TeX-source-correlate-mode t)
    (setq TeX-source-correlate-start-server t))



  (use-package preview
    :defer t
    :after latex
    :hook ((LaTeX-mode . preview-larger-previews))
    :config
    (defun preview-larger-previews ()
      (setq preview-scale-function
            (lambda () (* 0.8
                          (funcall (preview-scale-from-face)))))))


  (use-package reftex
    :defer t
    :after latex
    :hook (LaTeX-mode . turn-on-reftex)
    :custom
    (reftex-plug-into-AUCTeX t))


  (use-package aas
    :ensure t
    :defer t
    :hook (LaTeX-mode . aas-activate-for-major-mode)
    :hook (org-mode . aas-activate-for-major-mode)
    :config
    ;; (aas-set-snippets 'text-mode
    ;;   ;; expand unconditionally
    ;;   ";o-" "ō"
    ;;   ";i-" "ī"
    ;;   ";a-" "ā"
    ;;   ";u-" "ū"
    ;;   ";e-" "ē")
    ;; (aas-set-snippets 'latex-mode
    ;;   ;; set condition!
    ;;   :cond #'texmathp ; expand only while in math
    ;;   "supp" "\\supp"
    ;;   "On" "O(n)"
    ;;   "O1" "O(1)"
    ;;   "Olog" "O(\\log n)"
    ;;   "Olon" "O(n \\log n)"
    ;;   ;; Use YAS/Tempel snippets with ease!
    ;;   "amin" '(yas "\\argmin_{$1}") ; YASnippet snippet shorthand form
    ;;   "amax" '(tempel "\\argmax_{" p "}") ; Tempel snippet shorthand form
    ;;   ;; bind to functions!
    ;;   ";ig" #'insert-register
    ;;   ";call-sin"
    ;;   (lambda (angle) ; Get as fancy as you like
    ;;     (interactive "sAngle: ")
    ;;     (insert (format "%s" (sin (string-to-number angle))))))
    ;; disable snippets by redefining them with a nil expansion
    (aas-set-snippets 'latex-mode
      "supp" nil))


  (use-package laas
    :ensure t
    :defer t
    :hook (LaTeX-mode . laas-mode)
    :config ; do whatever here
    (aas-set-snippets 'laas-mode
      ;; set condition!
      :cond #'texmathp ; expand only while in math
      "supp" "\\supp"
      "On" "O(n)"
      "O1" "O(1)"
      "Olog" "O(\\log n)"
      "Olon" "O(n \\log n)"
      ;; bind to functions!
      "Sum" (lambda () (interactive)
              (yas-expand-snippet "\\sum_{$1}^{$2} $0"))
      "Span" (lambda () (interactive)
               (yas-expand-snippet "\\Span($1)$0"))
      ;; add accent snippets
      :cond #'laas-object-on-left-condition
      "qq" (lambda () (interactive) (laas-wrap-previous-object "sqrt"))))



  ;; ORGTBL → LaTeX / AMSMATH integration

  (add-hook 'LaTeX-mode-hook #'orgtbl-mode)

  (defun my-orgtbl-to-latex-rows ()
    "Convert the Org table at point into LaTeX rows (no tabular wrapper)."
    (let* ((table (org-table-to-lisp)))
      (mapconcat
       (lambda (row)
         (concat (mapconcat #'identity row " & ") " \\\\"))
       table
       "\n")))

  (defun my-orgtbl-convert-at-point (_arg)
    "Replace the current Org table with LaTeX rows."
    (interactive "P")
    (unless (org-at-table-p)
      (user-error "Cursor is not inside an Org table"))
    (let ((beg (org-table-begin))
          (end (org-table-end)))
      (let ((latex (my-orgtbl-to-latex-rows)))
        (delete-region beg end)
        (goto-char beg)
        (insert latex "\n"))))

  (with-eval-after-load 'org-table
    ;; Use orgtbl's built-in dispatch key
    ;; (define-key orgtbl-mode-map (kbd "C-c C-t ") #'my-orgtbl-convert-at-point)
    (define-key orgtbl-mode-map (kbd "C-c C-t C-t") #'my-orgtbl-convert-at-point)
    (define-key orgtbl-mode-map (kbd "C-c C-x") #'org-table-align))

#+end_src

#+RESULTS:
: org-table-align

* Magit

#+begin_src emacs-lisp
  (use-package transient :ensure t) ;; TODO investigate confilct with internal transient package 
  (use-package magit
    :defer t
    :ensure t) 

#+end_src

* Markdown
#+begin_src emacs-lisp

  (elpaca markdown-mode
    :defer t
    :ensure t)

#+end_src


* My Functions

#+begin_src emacs-lisp

    ;;; My Functions for Emacs  
      ;;;; Mode Line Toggle
  (defvar-local my--saved-mode-line-format mode-line-format
    "Backup of the mode line format for the current buffer.")

  (defun my/toggle-mode-line ()
    "Toggle mode line visibility for the current buffer only."
    (interactive)
    (if mode-line-format
        (progn
          (setq my--saved-mode-line-format mode-line-format)
          (setq mode-line-format nil))
      (setq mode-line-format my--saved-mode-line-format))
    (force-mode-line-update)
    (redraw-display))

    ;;;; Line Number toggle 
  (defvar-local my--saved-display-line-numbers nil
    "Internal storage for previous `display-line-numbers` value.")

  (defun my/toggle-line-numbers ()
    "Toggle line numbers, restoring the previous value when re-enabled."
    (interactive)
    (if display-line-numbers
        ;; Turning OFF
        (progn
          (setq my--saved-display-line-numbers display-line-numbers)
          (setq-local display-line-numbers nil))
      ;; Turning ON
      (setq-local display-line-numbers
                  (or my--saved-display-line-numbers t))))

    ;;;; Reading / Zen Mode
  (defun my/reading-mode ()
    "Enter or exit a distraction-free zen mode."
    (interactive)
    
    ;; Toggle mode-line
    (my/toggle-mode-line)
    
    ;; Toggle line numbers (state-preserving)
    (my/toggle-line-numbers))

    ;;;; Dired Sort Function
  (defun my/dired-sort ()
    "Sort dired listing interactively."
    (interactive)
    (let ((sort-options
           '(("date" . "-Al -t")                    ; newest first
             ("size" . "-Al -S")                    ; biggest first
             ("name" . "-Al --group-directories-first -v")  ; human-readable name sort
             ("dir"  . "-Al --group-directories-first")))  ; dirs first, then natural sort
          choice
          ls-switches)
      (setq choice (completing-read "Sort by (default date): "
                                    sort-options nil t nil nil "date"))
      (setq ls-switches (cdr (assoc choice sort-options)))
      (dired-sort-other ls-switches)))


  ;;;; Get Dired Directory Size 
  (defun my/dired-get-size ()
    "Get total size of all marked files/folders in dired"
    (interactive)
    (let ((files (dired-get-marked-files)))
      (with-temp-buffer
        (apply 'call-process "/usr/bin/du" nil t nil "-sch" files)
        (message "Size of all marked files: %s"
                 (progn 
                   (re-search-backward "\\(^[0-9.,]+[A-Za-z]+\\).*total$")
                   (match-string 1))))))


  (with-eval-after-load 'dired
  (define-key dired-mode-map (kbd "?") #'my/dired-get-size))

  (add-hook 'dired-mode-hook
            (lambda ()
              (evil-define-key 'normal dired-mode-map (kbd "?") #'my/dired-get-size)))



  ;;;; Consult Preview For Any buffer  
  (defun my/consult-preview ()
    "Previews candidate in vertico buffer, unless it's a consult command"
    (interactive)
    (unless (bound-and-true-p consult--preview-function)
      (save-selected-window
        (let ((embark-quit-after-action nil))
          (embark-dwim)))))

  (define-key minibuffer-local-map (kbd "M-.") #'my/consult-preview)
#+end_src

* ORG MODE

** Org Mode Source Block
Open source block editing buffer in new window
#+begin_src emacs-lisp
  (setq org-src-window-setup 'current-window)  ;; edits in the current window
#+end_src

** Org Tempo
Enables Shorthand for Different Blocks
Org-tempo is not a separate package but a module within org that can be enabled.  Org-tempo allows for '<s' followed by TAB to expand to a begin_src tag.  Other expansions available include:

| Typing the below + TAB | Expands to ...                          |
|------------------------+-----------------------------------------|
| <a                     | '#+BEGIN__{}EXPORT ascii' … '#+END__{}EXPORT  |
| <c                     | '#+BEGIN__{}CENTER' … '#+END__{}CENTER'       |
| <C                     | '#+BEGIN__{}COMMENT' … '#+END__{}COMMENT'     |
| <e                     | '#+BEGIN__{}EXAMPLE' … '#+END__{}EXAMPLE'     |
| <E                     | '#+BEGIN__{}EXPORT' … '#+END__{}EXPORT'       |
| <h                     | '#+BEGIN__{}EXPORT html' … '#+END__{}EXPORT'  |
| <l                     | '#+BEGIN__{}EXPORT latex' … '#+END__{}EXPORT' |
| <q                     | '#+BEGIN__{}QUOTE' … '#+END__{}QUOTE'         |
| <s                     | '#+BEGIN__{}SRC' … '#+END__{}SRC'             |
| <v                     | '#+BEGIN__{}VERSE' … '#+END__{}VERSE'         |

#+begin_src emacs-lisp
(require 'org-tempo)
#+end_src

**  Table of Contents
Use <Name> :toc: to add a Table of contents  to Emacs Org document 
#+begin_src emacs-lisp
  (use-package toc-org
    :ensure t(:wait t)  
    :commands toc-org-enable
    :init (add-hook 'org-mode-hook 'toc-org-enable))
#+end_src

** General Org Mode Settings
Here we set the following
- Hide Astericks in headings with =org-hide-leading-stars= and enabling =org-indent-mode= for better visual distinction of
  heading levels
- Make special symbols render as UTF-8 using =org-pretty-entities=.For quick cheetsheet *M-x* \rarr =org-entities-help= 
- Language support in Org Babel
- Fixing <s ~Org Tempo~ clash with =electric-pair-mode=

#+begin_src emacs-lisp
  (use-package org
    :defer t
    :hook (org-mode . (lambda ()
                        (org-indent-mode)
                        (setq-local electric-pair-inhibit-predicate
                                    (lambda (c)
                                      (if (char-equal c ?<)
                                          t
                                        (electric-pair-default-inhibit c))))))
    :config
    ;; Load languages
    (org-babel-do-load-languages
     'org-babel-load-languages
     '((emacs-lisp . t)
       (python . t)))
    ;; Hide leading asterisks in headings
    (setq org-hide-leading-stars t)
    ;; Pretty entities (nicer symbols)
    (setq org-pretty-entities t)
    ;; Skip "Execute?" dialog
    (setq org-confirm-babel-evaluate nil)
    ;; syntax highlighting for LaTex Fragments
    (setq org-highlight-latex-and-related '(latex script entities))
    ;; Hide emphasis markers (*bold* /italic/ =verbatim= etc.)
    (setq org-hide-emphasis-markers t))

#+end_src

#+begin_src emacs-lisp
  (use-package org-roam
    :defer t
    :ensure t
    :custom
    (org-roam-directory (file-truename "~/org-notes/"))
    :bind (("C-c n l" . org-roam-buffer-toggle)
           ("C-c n f" . org-roam-node-find)
           ("C-c n g" . org-roam-graph)
           ("C-c n i" . org-roam-node-insert)
           ("C-c n c" . org-roam-capture)
           ;; Dailies
           ("C-c n j" . org-roam-dailies-capture-today))
    :config
    ;; If you're using a vertical completion framework, you might want a more informative completion interface
    (setq org-roam-node-display-template (concat "${title:*} " (propertize "${tags:10}" 'face 'org-tag)))
    (org-roam-db-autosync-mode)
    ;; If using org-roam-protocol
    (require 'org-roam-protocol))
#+end_src

* Sane Defaults
** Fixing Emacs Wierdness
*** Backup ~ files
Useless
#+begin_src emacs-lisp
  (setq make-backup-files nil)
#+end_src
*** Ediff
#+begin_src emacs-lisp
  ;; Always use vertical split for diffs
  (setq ediff-split-window-function #'split-window-horizontally)
  ;; Never create a new frame for the control panel
  (setq ediff-window-setup-function #'ediff-setup-windows-plain)
#+end_src
*** Hippie Expand replacing Deabrev

#+begin_src emacs-lisp
(global-set-key [remap dabbrev-expand] 'hippie-expand)
#+end_src
*** Smooth Scrolling
Native in 29+ is the best 
#+begin_src emacs-lisp
#+end_src

*** Remember File Position

#+begin_src emacs-lisp
  (save-place-mode 1)
#+end_src

*** Restore Last Session
#+begin_src emacs-lisp
  (desktop-save-mode -1)
#+end_src

*** Auto load files changed on Disk
Automatically reverts buffers changed on disk
#+begin_src emacs-lisp
  (global-auto-revert-mode 1)
  ;; Helpful to load changed files in dired
  (setq global-auto-revert-non-file-buffers t)
#+end_src

*** Prevent using UI for conformation dialog
#+begin_src emacs-lisp
(setq use-dialog-box nil)
#+end_src

*** Avoid loading custom varable changes in init.el
#+begin_src emacs-lisp
;; Move customization variables to a separate file and load it
(setq custom-file (locate-user-emacs-file "custom-vars.el"))
(load custom-file 'noerror 'nomessage)
#+end_src

*** Clipboard Integration With System
#+begin_src emacs-lisp
  ;; Disable native clipboard to avoid Wayland/PGTK bugs after Emacs copies
  (setq select-enable-clipboard nil)
  (setq select-enable-primary nil)

  ;; Use wl-clipboard for reliable copy/paste on Wayland
  (setq wl-copy-process nil)
  (defun wl-copy (text)
    "Copy TEXT to Wayland clipboard using wl-copy."
    (setq wl-copy-process (make-process :name "wl-copy"
                                         :buffer nil
                                         :command '("wl-copy" "-f" "-n")
                                         :connection-type 'pipe
                                         :noquery t))
    (process-send-string wl-copy-process text)
    (process-send-eof wl-copy-process))
  (defun wl-paste ()
    "Paste from Wayland clipboard using wl-paste."
    (if (and wl-copy-process (process-live-p wl-copy-process))
        nil  ; Return nil if we own the clipboard (use kill-ring instead)
      (shell-command-to-string "wl-paste -n | tr -d \r")))
  (setq interprogram-cut-function 'wl-copy)
  (setq interprogram-paste-function 'wl-paste)
#+end_src

*** Tabs
#+begin_src emacs-lisp
  (setq-default tab-width 4)
#+end_src

*** Auto closing of pair delimeters (AutoClose)
#+begin_src emacs-lisp
  (electric-pair-mode 1)
#+end_src

*** Tab Bar
#+begin_src emacs-lisp
  (setq-default tab-bar-show 1)
#+end_src

*** Uniquify
Buffer with same name get better samrt names
#+begin_src emacs-lisp
  (use-package uniquify
    :ensure nil 
    :config
    (setq uniquify-buffer-name-style 'post-forward)
    (setq uniquify-separator " | ")
    (setq uniquify-after-kill-buffer-p t)
    (setq uniquify-ignore-buffers-re "^\\*"))
#+end_src


** Treesitter
- Color me impressed
#+begin_src emacs-lisp
  ;; Define languages to install
  (setq treesit-language-source-alist
        '((python "https://github.com/tree-sitter/tree-sitter-python")
          (javascript "https://github.com/tree-sitter/tree-sitter-javascript")
          (typescript "https://github.com/tree-sitter/tree-sitter-typescript")
          (c       "https://github.com/tree-sitter/tree-sitter-c")
  		(lua "https://github.com/tree-sitter-grammars/tree-sitter-lua")
          (cpp     "https://github.com/tree-sitter/tree-sitter-cpp")
          (rust    "https://github.com/tree-sitter/tree-sitter-rust")
          (go      "https://github.com/tree-sitter/tree-sitter-go")
  		(julia  "https://github.com/tree-sitter/tree-sitter-julia")
          (java    "https://github.com/tree-sitter/tree-sitter-java")))

  (setq treesit-font-lock-level 4)
  (use-package treesit-auto
      :ensure t
      :custom
      (treesit-auto-install 'prompt)
      :config
      (treesit-auto-add-to-auto-mode-alist 'all)
      (global-treesit-auto-mode))
#+end_src

#+RESULTS:
: [nil 26967 50600 232407 nil elpaca-process-queues nil nil 256000 nil]


* Vterm
c good 
#+begin_src emacs-lisp
  (use-package vterm
    :defer t
    :ensure t)
#+end_src

* Vundo
Visualize undo tree
#+begin_src emacs-lisp
  (use-package vundo
    :ensure t
    :bind ("C-x u" . vundo)
    :config
    ;; Take less on-screen space.
    (setq vundo-compact-display t)
    ;; Use `HJKL` VIM-like motion, also Home/End to jump around.
    (define-key vundo-mode-map (kbd "l") #'vundo-forward)
    (define-key vundo-mode-map (kbd "<right>") #'vundo-forward)
    (define-key vundo-mode-map (kbd "h") #'vundo-backward)
    (define-key vundo-mode-map (kbd "<left>") #'vundo-backward)
    (define-key vundo-mode-map (kbd "j") #'vundo-next)
    (define-key vundo-mode-map (kbd "<down>") #'vundo-next)
    (define-key vundo-mode-map (kbd "k") #'vundo-previous)
    (define-key vundo-mode-map (kbd "<up>") #'vundo-previous)
    (define-key vundo-mode-map (kbd "<home>") #'vundo-stem-root)
    (define-key vundo-mode-map (kbd "<end>") #'vundo-stem-end)
    (define-key vundo-mode-map (kbd "q") #'vundo-quit)
    (define-key vundo-mode-map (kbd "C-g") #'vundo-quit)
    (define-key vundo-mode-map (kbd "RET") #'vundo-confirm))
#+end_src


* Zoxide
#+begin_src emacs-lisp

  (use-package zoxide
    :ensure t
    :init
    (global-set-key (kbd "C-x C-j") 'zoxide-find-file))
#+end_src

* Keybindings
** EMACS Specific
Using Emacs-Style keybindings
*** Global

#+begin_src emacs-lisp
  ;;; Emacs Specifc Binidngs 
  (global-set-key (kbd "C-=") 'text-scale-increase)
  (global-set-key (kbd "C--") 'text-scale-decrease)
  (global-set-key (kbd "<C-wheel-up>") 'text-scale-increase)
  (global-set-key (kbd "<C-wheel-down>") 'text-scale-decrease)
  (global-set-key (kbd "<C-tab>") #'mode-line-other-buffer)
  (global-set-key (kbd "C-x C-c") #'kill-buffer-and-window)
  (global-set-key (kbd "C-c u") #'insert-char)
  (global-set-key (kbd "C-<return>") #'eval-print-last-sexp)
  (global-set-key (kbd "C-x g") #'magit)
  (global-set-key (kbd "C-x C-z") #'my/reading-mode)
  (global-set-key (kbd "C-c e") #'evil-mode)
#+end_src

*** Mode Specific

** EVIL Mode Specific 
*** Global
**** Insert Mode
#+begin_src emacs-lisp
  ;; Keybindings section
  ;; jj to escape insert mode
  (use-package key-chord
    :ensure t
    :config
    (key-chord-mode 1)
    (key-chord-define evil-insert-state-map "jj" 'evil-normal-state))

  (evil-global-set-key 'insert (kbd "C-/") #'cape-file)
  (evil-global-set-key 'insert (kbd "C-<return>") #'eval-print-last-sexp)
#+end_src

**** Normal Mode
#+begin_src emacs-lisp

  ;; Space as leader key in normal and visual modes
  (evil-set-leader
   '(normal visual motion)
   (kbd "SPC"))

  (evil-global-set-key 'normal (kbd "<leader>ff") #'consult-find)
  (evil-global-set-key 'normal (kbd "<leader>fg") #'consult-ripgrep)
  (evil-global-set-key 'normal (kbd "<leader>fS") #'consult-locate)
  (evil-global-set-key 'normal (kbd "<leader>fb") #'consult-buffer)
  (evil-global-set-key 'normal (kbd "<leader>fr") #'consult-recent-file)
  (evil-global-set-key 'normal (kbd "<leader>df") #'consult-flymake)
  (evil-global-set-key 'normal (kbd "<leader>fd") #'consult-imenu)
  (evil-global-set-key 'normal (kbd "<leader>fD") #'consult-imenu-multi)
  (evil-global-set-key 'normal (kbd "<leader>fo") #'consult-outline)
  (evil-global-set-key 'normal (kbd "<leader>yY") #'consult-yank-from-kill-ring)
  (evil-global-set-key 'normal (kbd "<leader>xf") #'find-file)
  (evil-global-set-key 'normal (kbd "<leader>th") #'consult-theme)
  (evil-global-set-key 'normal (kbd "<leader>bb") #'consult-buffer)
  (evil-global-set-key 'normal (kbd "<leader>de") #'consult-dir)
  (evil-global-set-key 'normal (kbd "<leader>wd") #'delete-window)
  (evil-global-set-key 'normal (kbd "<leader>yy") #'consult-yank-from-kill-ring)
  (evil-global-set-key 'normal (kbd "<leader>bk") #'kill-current-buffer)
  (evil-global-set-key 'normal (kbd "<leader>ib") #'ibuffer)
  (evil-global-set-key 'normal (kbd "<leader>bs") #'save-buffer)
  (evil-global-set-key 'normal (kbd "<leader>dj") #'dired-jump)
  (evil-global-set-key 'normal (kbd "<leader>cd") #'zoxide-travel)
  (evil-global-set-key 'normal (kbd "<leader>fj") #'zoxide-find-file)
  (evil-global-set-key 'normal (kbd "<leader>vt") #'vterm)
  (evil-global-set-key 'normal (kbd "<leader>cf") #'eglot-format)
  (evil-global-set-key 'normal (kbd "<leader>er") #'eglot-rename)
  (evil-global-set-key 'normal (kbd "<leader> SPC") #'execute-extended-command)
  (evil-global-set-key 'normal (kbd "<leader>wq") #'kill-buffer-and-window)
  (evil-global-set-key 'normal (kbd "<leader>cl") #'consult-line)
  (evil-global-set-key 'normal (kbd "<leader>cL") #'consult-line-multi)
  (evil-global-set-key 'normal (kbd "<leader>gg") #'consult-goto-line)
  (evil-global-set-key 'normal (kbd "<leader>fl") #'consult-focus-lines)
  (evil-global-set-key 'normal (kbd "<leader>pp") #'project-switch-project)
  (evil-global-set-key 'normal (kbd "<leader>pb") #'consult-project-buffer)
  (evil-global-set-key 'normal (kbd "<leader>pd") #'project-find-dir)
  (evil-global-set-key 'normal (kbd "<leader>pf") #'project-find-file)
  (evil-global-set-key 'normal (kbd "<leader>pD") #'project-dired)
  (evil-global-set-key 'normal (kbd "<leader>ps") #'project-shell)
  (evil-global-set-key 'normal (kbd "<leader>pk") #'project-kill-buffers)
  (evil-global-set-key 'normal (kbd "<leader>pr") #'project-shell-command)
  (evil-global-set-key 'normal (kbd "<leader>mm") #'consult-bookmark)
  (evil-global-set-key 'normal (kbd "<leader>mi") #'bookmark-insert)
  (evil-global-set-key 'normal (kbd "<leader>ml") #'bookmark-insert-location)
  (evil-global-set-key 'normal (kbd "<leader>mj") #'bookmark-bmenu-list)
  (evil-global-set-key 'normal (kbd "<leader>ic") #'insert-char)
  (evil-global-set-key 'normal (kbd "<leader>is") #'ispell)
  (evil-global-set-key 'normal (kbd "<leader>mg") #'magit)
  (evil-global-set-key 'normal (kbd "<leader>em") #'elpaca-manager)
  (evil-global-set-key 'normal (kbd "<leader>xnn") #'narrow-to-region)
  (evil-global-set-key 'normal (kbd "<leader>xnp") #'narrow-to-page)
  (evil-global-set-key 'normal (kbd "<leader>xnd") #'narrow-to-defun)
  (evil-global-set-key 'normal (kbd "<leader>xnw") #'widen)
  (evil-global-set-key 'normal (kbd "<leader>sl") #'imenu)
  (evil-global-set-key 'normal (kbd "<leader>si") #'yas-insert-snippet)
  (evil-global-set-key 'normal (kbd "<leader>sn") #'yas-new-snippet)
  (evil-global-set-key 'normal (kbd "<leader>se") #'yas-visit-snippet-file)
  (evil-global-set-key 'normal (kbd "K") #'eldoc-box-help-at-point)

  (with-eval-after-load 'evil
    (evil-define-key* '(normal insert) global-map
      (kbd "C-j")
      (lambda ()
        (interactive)
        (if (and (fboundp 'eldoc-box--frame-visible-p)
                 (eldoc-box--frame-visible-p))
            (eldoc-box-scroll-up 4)
          (evil-next-visual-line))))

    (evil-define-key* '(normal insert) global-map
      (kbd "C-k")
      (lambda ()
        (interactive)
        (if (and (fboundp 'eldoc-box--frame-visible-p)
                 (eldoc-box--frame-visible-p))
            (eldoc-box-scroll-down 4)
          (evil-previous-visual-line)))))

  ;; Org-mode specific bindings
  (evil-global-set-key 'normal (kbd "<leader>oa") #'org-agenda)
  (evil-global-set-key 'normal (kbd "<leader>oc") #'org-capture)
  #+end_src

**** Visual Mode
#+begin_src emacs-lisp

#+end_src


*** Mode Specific 
**** Org Mode
#+begin_src emacs-lisp
    ;; Org-mode specific bindings (only in org-mode buffers)
    (with-eval-after-load 'org
      (evil-define-key 'normal org-mode-map (kbd "<leader>cb") #'org-toggle-checkbox)
      (evil-define-key 'normal org-mode-map (kbd "<leader>oh") #'org-toggle-heading)
  	)
#+end_src



* UI 

** Theme
Load theme in early-init to avoid UI loading with light theme
#+begin_src emacs-lisp
  ;; (load-theme 'modus-vivendi t)
#+end_src


** FONTS
Defining the various fonts that Emacs will use.

#+begin_src emacs-lisp

  ;; Set default font for high-DPI monitor
  (set-face-attribute 'default nil
                      :family "Cascadia Code NF "   ;; only use monospace other fonts break org mode 
                      :height 180)                  ;; font size in 1/10 pt (160 = 16pt)

  ;; Optional: set fixed-pitch for code blocks
  (set-face-attribute 'fixed-pitch nil
                      :height 160)

  ;; Make comments italic
  (custom-set-faces
   '(font-lock-comment-face ((t (:slant italic)))))
#+end_src




** GRAPHICAL USER INTERFACE TWEAKS

*** Disable Menubar, Toolbars and Scrollbars

#+begin_src emacs-lisp
  (menu-bar-mode -1)
  (tool-bar-mode -1)
  (scroll-bar-mode -1)
#+end_src


*** Display Line Numbers and Truncated Lines
#+begin_src emacs-lisp
  (setq-default display-line-numbers 'visual ;; behaves well with folds
    	    display-line-numbers-current-absolute t)
  (setq-default truncate-lines t)

#+end_src

* Testing
#+begin_src emacs-lisp
  (setq yas-wrap-around-region t)
#+end_src
